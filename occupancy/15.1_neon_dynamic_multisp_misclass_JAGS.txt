model {

  #AIS what's the ideal way to initialize occupancy parameters?

  # Parameter argument priors
  mu_psi1   ~ dnorm(0,1)
  mu_p      ~ dnorm(0,1)
  mu_phi    ~ dnorm(1.5,1)
  mu_gamma  ~ dnorm(-2,1)
  
  sd_psi1   ~ dunif(0, 10)
  sd_p      ~ dunif(0, 10)
  sd_phi    ~ dunif(0, 10)
  sd_gamma  ~ dunif(0, 10)
  
  tau_psi1  <- pow(sd_psi1, -2)
  tau_p     <- pow(sd_p, -2)
  tau_phi   <- pow(sd_phi, -2)
  tau_gamma <- pow(sd_gamma, -2)
  
  for (k in 1:K_exp) {
    e_psi1[k]  ~ dnorm(0,tau_psi1)
    e_p[k]     ~ dnorm(0,tau_p)
    e_phi[k]   ~ dnorm(0,tau_phi)
    e_gamma[k] ~ dnorm(0,tau_gamma)
  
    logit(psi1[k])  = mu_psi1 + e_psi1[k]
    logit(p[k])     = mu_p + e_p[k]
    logit(phi[k])   = mu_phi + e_phi[k]
    logit(gamma[k]) = mu_gamma + e_gamma[k]
    
    Theta[k,1:K_para] ~ ddirch(alpha[k,1:K_para])
    
    for (t in 1:nyear) {
      log_lambda[k, t] ~ dnorm(1, 1)
      lambda[k, t] = exp(log_lambda[k, t])
    }
  }

  for (i in 1:nsite) {
    for (k in 1:K_exp) {
      z[i, k, 1] ~ dbern(psi1[k])
      for (t in 2:nyear) {
        z[i, k, t]  ~ dbern(z[i,k,t-1]*phi[k] + 
                        (1 - z[i,k,t-1])*gamma[k]) 
      }
      for (t in 1:nyear) {
        zlam[i, k, t] = z[i, k, t] * lambda[k, t]
      }
    }
  }
  
  for (i in 1:nsite) {
    for (j in 1:nsurv) {
      for (t in 1:nyear) {
          L[i, j, t] ~ dpois(sum(zlam[i, 1:K_exp, t]))
      }
    }
  }

  for (l in 1:Ltot) {
    pi[l, 1:K_exp] = zlam[site[l], 1:K_exp, year[l]] / sum(zlam[site[l], 1:K_exp, year[l]])
    k[l] ~ dcat(pi[l, 1:K_exp])
    y[l] ~ dcat(Theta[k[l], 1:K_para])
  }
  
  # Derived parameters
  for (k in 1:K_exp) {
    psi[k,1] <- psi1[k]
    for (t in 2:nyear) {
      psi[k,t]        = psi[k,t-1]*phi[k] + 
                          (1 - psi[k,t-1])*gamma[k] 
      growth[k,t]     = psi[k,t]/psi[k,t-1] 
      turnover[k,t-1] = (1 - psi[k,t-1])*gamma[k]/psi[k,t] 
    }
  }
}
